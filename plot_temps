#====================================================================
# plot_temps
#
# gnuplot script to plot daily temperature and heating history
#====================================================================
set term png transparent truecolor enhanced size 1000,800 font "Arial, 12"
set object rectangle from screen 0,0 to screen 1,1 behind fillcolor rgb 'white' fillstyle solid noborder
if (!exists("plot_title")) plot_title = strftime("%Y%m%d", time(0))
set title plot_title
set output "temp.png"
set key right bottom box opaque
set xdata time
set timefmt "%Y-%m-%d %H:%M:%S"
set xtics rotate 
set format x "%H"
set format y "%6.2f" 
set grid ytics lt 0 lw 1 lc rgb "#bbbbbb"
set grid xtics lt 0 lw 1 lc rgb "#bbbbbb"
set xlabel "TIME"
set ylabel "TEMP (F)"
set ytics nomirror
set y2range [0:1]
set style line 1 lt 1 lc rgb "#FA7A0A" lw 3
set style line 2 lt 1 lc rgb "#474AFF" lw 3
set style line 3 lt 1 lc rgb "#AA0000" lw 3
set style line 4 lt 1 lc rgb "#7D7D7D" lw 3
set style line 5 lt 1 lc rgb "#0DFF00" lw 3
set style fill transparent solid 0.5 noborder
plot  \
 "temp.dat" using 1:5 with filledcurves below fs transparent solid 0.10 lc rgb 'red' axes x1y2 t "HEATING", \
 "temp.dat" using 1:4 with lines ls 5 t "SET POINT"  , \
 "temp.dat" using 1:6 with lines ls 2 t "OUTSIDE" , \
 "temp.dat" using 1:3 with lines ls 1 t "INSIDE" 
exit
